plugins {
	id 'java-library'
	id "idea"
	id "jacoco"
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "com.amazonaws:aws-java-sdk-dynamodb:1.12.669",
			"software.amazon.awssdk:dynamodb-enhanced:2.25.11",
			"software.amazon.lambda:powertools-parameters:1.16.1",
			"software.amazon.awssdk:kms:2.25.17",
			"org.apache.logging.log4j:log4j-api:2.23.0",
			"org.apache.logging.log4j:log4j-core:2.23.0"

	testImplementation "org.junit.jupiter:junit-jupiter:5.10.2",
			"org.mockito:mockito-junit-jupiter:5.4.0",
			'uk.org.webcompere:system-stubs-jupiter:2.1.6'
	testRuntimeOnly("org.junit.platform:junit-platform-launcher")

	compileOnly "org.projectlombok:lombok:1.18.30"
	annotationProcessor "org.projectlombok:lombok:1.18.30"
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(Jar).configureEach { Jar jar ->
	jar.preserveFileTimestamps = false
	jar.reproducibleFileOrder = true
}

tasks.named('jar') {
	manifest {
		attributes('Implementation-Title': project.name,
		'Implementation-Version': project.version)
	}
}

test {
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)
	}
}
